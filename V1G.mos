model V1G !without discharging with control model
  uses "mmxprs"

  declarations
    number_of_time_periods = 24
    time_period = 1.. number_of_time_periods
    time_period_names : array(time_period) of string

    number_of_EVs = 12
    EV = 1.. number_of_EVs
    EV_names : array(EV) of string

    charger : real !integer
    price_of_charging : array(time_period) of real

    working_time : array(EV,time_period) of real
    working_energy_cost : array(EV) of real

    charge_power_max : array(time_period) of real

    battery_max : array(EV) of real
    battery_min : array(EV) of real
    battery_for_work : array(EV) of real

    charging_rate : real

    charging_status : array(EV,time_period) of mpvar

    !SOC limitations
    eta : real
    initial_SOC : real
    SOC_level : array(EV,time_period) of mpvar

    !Objective:linctr
  end-declarations

  initialisations from "Dissertation.dat"
    time_period_names EV_names charger price_of_charging working_time working_energy_cost charge_power_max battery_max battery_min battery_for_work
    charging_rate eta initial_SOC
  end-initialisations

  !binary
  forall(i in EV, t in time_period) do
    charging_status(i,t) is_binary
  end-do

  !constraints

  !state of charge
  forall(i in EV) do
    if (working_time(i,1) = 1) then
      SOC_level(i,1) = initial_SOC + eta*charging_status(i,1)*charging_rate
    end-if
    if (working_time(i,1) = 0) then
      SOC_level(i,1) = initial_SOC - working_energy_cost(i)
    end-if
  end-do

  forall(i in EV, t in time_period) do
    if(2 <= t and t <= number_of_time_periods) then
      if (working_time(i,t) = 1) then
        SOC_level(i,t) = SOC_level(i,t-1) + eta*charging_status(i,t)*charging_rate
      end-if
      if (working_time(i,t) = 0) then
        SOC_level(i,t) = SOC_level(i,t-1) - working_energy_cost(i)
      end-if
    end-if
  end-do

  !by the end of day back to initial SOC
  forall(i in EV) do
    SOC_level(i, number_of_time_periods) >= initial_SOC
  end-do

  !charging limit at time period t
  forall(t in time_period) do
    sum(i in EV) charging_status(i,t)*charging_rate <= charge_power_max(t)
  end-do

  !charging battery capacity
  forall(i in EV, t in time_period) do
    SOC_level(i,t) <= battery_max(i)
  end-do

  !charging only at not working time
  forall(i in EV, t in time_period) do
    charging_status(i,t) <= working_time(i,t)
  end-do

  !battery capacity limit before out to work
  forall(i in EV, t in time_period) do
    if(t+1 <= number_of_time_periods) then
      if(working_time(i,t+1) = 0) then
        SOC_level(i,t) >= battery_for_work(i)
      end-if
    end-if
  end-do

  !charger limit
  forall(t in time_period) do
    sum(i in EV) charging_status(i,t) <= charger
  end-do

  !objective function
  obj := sum(t in time_period, i in EV) (charging_status(i,t)*charging_rate*price_of_charging(t))

  minimise(obj)

  fopen("out_V1G.txt",F_OUTPUT)
  forall(i in EV) do
    write("The initial SOC of ", EV_names(i), " is ", initial_SOC, ".")
    writeln
    forall(t in time_period) do
      if(working_time(i,t)=1) then
        if(getsol(charging_status(i,t))=1) then
        write("At time period ", time_period_names(t), ", ", EV_names(i), " is charging")
        end-if
        if(getsol(charging_status(i,t))=0) then
        write("At time period ", time_period_names(t), ", ", EV_names(i), " has nothing to do")
        end-if
      else
      write("At time period ", time_period_names(t), ", ", EV_names(i), " is out to work")
      end-if
    write(", and the state of charge is ", getsol(SOC_level(i,t)), " .")
    writeln
    end-do
    writeln
  end-do

  writeln("The minimum cost is ",getobjval,".")
  fclose(F_OUTPUT)

  fopen("out_V1G.csv", F_OUTPUT)
    write("Status",",")
    forall(t in time_period) do
    write(time_period_names(t), ",")
    end-do
    writeln
    forall(i in EV) do
    write(EV_names(i), ",")
      forall(t in time_period) do
        if(working_time(i,t)=1) then
          if(getsol(charging_status(i,t))=1) then
          write("Charge", ",")
          end-if
          if(getsol(charging_status(i,t))=0) then
          write("Nothing", ",")
          end-if
        else
        write("Delivery", ",")
        end-if
      end-do
      writeln
    end-do
    writeln
    write("SOC",",")
    forall(t in time_period) do
    write(time_period_names(t), ",")
    end-do
    writeln
    forall(i in EV) do
      write(EV_names(i), ",")
      forall(t in time_period) do
      write(getsol(SOC_level(i,t)), ",")
      end-do
      writeln
    end-do
  fclose(F_OUTPUT)
end-model

